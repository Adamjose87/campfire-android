apply plugin: "com.android.application"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"

android {
    compileSdkVersion 'android-P'
    defaultConfig {
        applicationId "com.pandulapeter.campfire"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "0.0.3"
        vectorDrawables.useSupportLibrary = true
    }
    dataBinding {
        enabled = true
    }
    signingConfigs {
        internal {
            keyAlias "androiddebugkey"
            keyPassword "android"
            storeFile file("internal.keystore")
            storePassword "android"
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        internal {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.internal
            applicationIdSuffix ".internal"
            versionNameSuffix "-internal"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            //TODO: Create a separate signing configuration for Release builds.
            signingConfig signingConfigs.internal
        }
    }
}
repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://maven.google.com"
    }
}
kotlin {
    experimental {
        coroutines "enable"
    }
}
androidExtensions {
    experimental = true
}
dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$rootProject.kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.19.2"

    // Dependency injection
    implementation "org.koin:koin-android:0.8.0"

    // Networking
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    // Persistence
    implementation "android.arch.persistence.room:runtime:$roomVersion"
    kapt "android.arch.persistence.room:compiler:$roomVersion"

    // Support library
    //noinspection GradleDependency,GradleCompatible
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    //noinspection GradleDependency,GradleCompatible
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    //noinspection GradleDependency,GradleCompatible
    implementation "com.android.support:design:$supportLibraryVersion"
    //noinspection GradleDependency,GradleCompatible
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support.constraint:constraint-layout:1.1.0"
}
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        // Blockers in newer support library versions: app bar snapping bug and snackbar swipe behavior.
        if (details.requested.group == "com.android.support") {
            details.useVersion "$supportLibraryVersion"
        }
        // Blockers in newer Kotlin versions: Room
        if (details.requested.name == 'kotlin-compiler-embeddable') {
            details.useVersion "1.2.30"
        }
    }
}